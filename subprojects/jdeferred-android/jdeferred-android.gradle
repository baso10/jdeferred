/*
 * Copyright 2013-2016 Ray Tsang
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'maven-publish'

def aarPomConfig = {
    name project.name
    description project.project_description
    url project.project_url
    inceptionYear '2013'
    licenses {
        license([:]) {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    scm {
        url project.project_scm
    }
    developers {
        developer {
            id('saturnism')
            name('Ray Tsang')
            roles {
                role('Developer')
            }
        }
        developer {
            id('aalmiray')
            name('Andres Almiray')
            roles {
                role('Developer')
            }
        }
    }
}

publishing {
    publications {
        mavenApklib(MavenPublication) {
            artifactId(project.name + '-apklib')
            artifact(apklib)
            pom.withXml {
                asNode().children().last() + aarPomConfig
                asNode().appendNode('description', project.project_description)
                def depNode = asNode().appendNode('dependencies')
                        .appendNode('dependency')
                depNode.appendNode('groupId', project.group)
                depNode.appendNode('artifactId', 'jdeferred-core')
                depNode.appendNode('version', project.version)
            }
        }
        mavenAar(MavenPublication) {
            artifactId(project.name + '-aar')
            artifact(bundleRelease)
            pom.withXml {
                asNode().children().last() + aarPomConfig
                asNode().appendNode('description', project.project_description)
                def depNode = asNode().appendNode('dependencies')
                        .appendNode('dependency')
                depNode.appendNode('groupId', project.group)
                depNode.appendNode('artifactId', 'jdeferred-core')
                depNode.appendNode('version', project.version)
            }
        }
    }
}

dependencies {
    compile project(':jdeferred-core')
    //compileOnly "com.google.android:android:$androidVersion"
    //testCompileOnly "com.google.android:android-test:$androidVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    testCompile "org.awaitility:awaitility:$awaitilityVersion"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"
    lintOptions {
        abortOnError false
    }
}

task apklib(type: Zip) {
    extension = 'apklib'

    from file('src/main/AndroidManifest.xml')
    into('res') {
        from 'res'
    }
    into('src') {
        from 'src'
    }
}

apply from: rootProject.file('gradle/code-quality.gradle')
